buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'net.researchgate.release' version '2.4.0'
    id "com.jfrog.bintray" version "1.7"
    id "com.sourcemuse.mongo" version "0.13.0"
}

allprojects {
	repositories {
		jcenter()
	}
	apply plugin: 'maven'
	apply plugin: 'maven-publish'
	apply plugin: 'java'
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'mongo'

jar {
    baseName = 'quartz-mongodb'
    version =  version
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

configurations {
    provided
    compile.extendsFrom provided
}
sourceSets {
    main { compileClasspath += configurations.provided }
}

idea {
    module {
        scopes.PROVIDED.plus += [configurations.provided]
    }
}
dependencies {
    compile("org.quartz-scheduler:quartz:2.2.3")
    compile("org.mongodb:mongodb-driver:3.4.0")
    compile("commons-codec:commons-codec:1.10")
    compile("org.slf4j:slf4j-api:1.7.21")
    provided("org.clojure:clojure:1.7.0")

    testCompile("joda-time:joda-time:2.8.2")
    testCompile("org.slf4j:slf4j-simple:1.7.10")
    testCompile("org.codehaus.groovy:groovy-all:2.4.1")
    testCompile("org.spockframework:spock-core:1.0-groovy-2.4")
    testRuntime("cglib:cglib-nodep:3.1")
    testRuntime("org.objenesis:objenesis:2.1")
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}


release {
    scmAdapters = [net.researchgate.release.GitAdapter]
    git {
        pushToRemote = 'origin'
    }
}

task sourceJar(type: Jar) {
    dependsOn classes
    classifier "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives sourceJar
}


publishing {
    publications {
        QuartzMongoDB(MavenPublication) {
            from components.java
            groupId 'com.novemberain'
            artifactId(jar.baseName)
            version(version)
        }
    }
}

tasks.withType(Test){
    runWithMongoDb = true
    mongo {
        journalingEnabled true
        port 12345
        logging 'console'
        mongoVersion '2.6.6'
    }
}

bintray {
    user = System.getenv("BINTRAY_USERNAME")
    key  = System.getenv("BINTRAY_API_KEY")
    publications = ['QuartzMongoDB']
    pkg {
        repo = 'maven'
        name = 'quartz-mongodb'
        labels = ['quartz', 'mongodb']
        userOrg = 'michaelklishin'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/michaelklishin/quartz-mongodb.git'

        version {
            name = version
			      desc = 'MongoDB-backed job store for the Quartz scheduler'
			      vcsTag = version
        }
    }
}
